# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//flutter/build/dart/rules.gni")
import("//flutter/tools/fuchsia/dart/dart_library.gni")

application_snapshot("kernel_compiler") {
  main_dart = "compiler.dart"

  deps = [ "../flutter/kernel:kernel_platform_files($host_toolchain)" ]

  package_config =
      rebase_path("//third_party/dart/.dart_tool/package_config.json")

  training_args = [
    "--train",
    rebase_path(main_dart),
  ]

  kernel_compiler_files =
      exec_script("//third_party/dart/tools/list_dart_files.py",
                  [
                    "absolute",
                    rebase_path("."),
                  ],
                  "list lines")

  kernel_compiler_files +=
      exec_script("//third_party/dart/tools/list_dart_files.py",
                  [
                    "absolute",
                    rebase_path("//third_party/dart/pkg"),
                  ],
                  "list lines")

  inputs = kernel_compiler_files
}

application_snapshot("_list_libraries_kernel") {
  visibility = [ ":*" ]
  snapshot_kind = "kernel"
  main_dart = "//third_party/dart/pkg/vm/bin/list_libraries.dart"
  package_config =
      rebase_path("//third_party/dart/.dart_tool/package_config.json")
  training_args = []
  output = "$target_gen_dir/list_libraries.dart.dill"
}

application_snapshot("list_libraries") {
  deps = [ ":_list_libraries_kernel" ]
  main_dart = "//third_party/dart/pkg/vm/bin/list_libraries.dart"
  package_config =
      rebase_path("//third_party/dart/.dart_tool/package_config.json")
  training_args = [
    # train against the dill file which is generated for this snapshot.
    rebase_path("$target_gen_dir/list_libraries.dart.dill"),
  ]
}

dart_library("async_helper") {
  package_root = "//third_party/dart/pkg/async_helper"
  package_name = "async_helper"

  language_version = "2.12"

  deps = []

  sources = [
    "async_helper.dart",
    "async_minitest.dart",
  ]
}

dart_library("expect") {
  package_root = "//third_party/dart/pkg/expect"
  package_name = "expect"

  language_version = "2.12"

  deps = [ ":meta" ]

  sources = [
    "expect.dart",
    "minitest.dart",
  ]
}

dart_library("litetest") {
  package_root = "//flutter/testing/litetest"
  package_name = "litetest"

  language_version = "2.12"

  source_dir = "lib"

  deps = [
    ":async_helper",
    ":expect",
    ":meta",
  ]

  sources = [
    "litetest.dart",
    "src/matchers.dart",
    "src/test.dart",
    "src/test_suite.dart",
  ]
}

dart_library("meta") {
  package_root = "//third_party/dart/pkg/meta"
  package_name = "meta"

  language_version = "2.12"

  deps = []

  sources = [
    "dart2js.dart",
    "meta.dart",
    "meta_meta.dart",
  ]
}
